#version 450 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D imgOutput;
layout(binding = 1) uniform sampler2D uDiff;
layout(binding = 2) uniform sampler2D uRough;
layout(binding = 3) uniform sampler2D uNorm;

layout(location = 0) uniform float uTime;
layout(location = 1) uniform ivec2 uRes;
layout(location = 2) uniform mat3 uCamMat;
layout(location = 3) uniform vec3 uCamPos;
layout(location = 4) uniform float uFOV;
layout(location = 5) uniform float uMaxBounce;
layout(location = 6) uniform float uRayPerPixel;

struct Sphere {
    vec4 origin;
    vec4 color;
    vec4 info;
};

layout(std430, binding = 1) buffer ssbo {
    Sphere objects[];
};

struct Cube {
    vec4 min;
    vec4 max;
    vec4 color;
    vec4 info;
};

layout(std430, binding = 2) buffer ssbo_cube {
    Cube cubes[];
};

struct Ray {
    vec3 origin;
    vec3 dir;
};

struct HitInfo {
    bool didHit;
    float dst;
    vec3 hitPoint;
    vec2 uv;
    vec3 normal;
    vec4 color;
    vec4 info;
};

float RandomValue(inout uint state) {
    state = state * 747796405 + 2891336453;
    uint result = ((state >> ((state >> 28) + 4)) ^ state) * 277803737;
    result = (result >> 22) ^ result;
    return result / 4294967295.0;
}

float RandomValueNormalDistribution(inout uint state) {
    float theta = 2 * 3.1415926 * RandomValue(state);
    float rho = sqrt(-2 * log(RandomValue(state)));
    return rho * cos(theta);
}

vec3 RandomDirection(inout uint state) {
    float x = RandomValueNormalDistribution(state);
    float y = RandomValueNormalDistribution(state);
    float z = RandomValueNormalDistribution(state);
    return normalize(vec3(x, y, z));
}

vec3 RandomHemisphereDirection(vec3 normal, inout uint rngState) {
    vec3 dir = RandomDirection(rngState);
    return dir * sign(dot(normal, dir));
}

vec2 RandomPointInCircle(inout uint rngState) {
    float angle = RandomValue(rngState) * 2 * 3.1415926;
    vec2 pointOnCircle = vec2(cos(angle), sin(angle));
    return pointOnCircle * sqrt(RandomValue(rngState));
}

vec2 getSphereUV(vec3 hitPoint, vec3 sphereCenter) {
    vec3 localP = hitPoint - sphereCenter;
    // Normalize the local point to ensure it's on the sphere surface
    vec3 n = normalize(localP);

    // Calculate spherical coordinates
    float u = 0.5 + atan(n.z, n.x) / (2.0 * 3.14159);
    float v = 0.5 - asin(n.y) / 3.14159;

    return vec2(u, v);
}

vec3 applyNormalMap(vec3 norm, vec2 uv) {
    // Sample the normal map and decode it to tangent space
    vec3 normalMapColor = texture(uNorm, uv).rgb;
    vec3 tangentNormal = normalize(normalMapColor * 2.0 - 1.0);

    // Calculate the TBN matrix
    vec3 tangent, bitangent;

    if (abs(norm.y) > 0.999) { // Top or bottom face (normal aligned with Y-axis)
        tangent = vec3(1.0, 0.0, 0.0); // Lock tangent to X-axis
        bitangent = vec3(0.0, 0.0, norm.y); // Align bitangent with Z-axis (depends on up/down)
    } else {
        tangent = normalize(vec3(-norm.z, 0.0, norm.x)); // General tangent
        bitangent = normalize(cross(norm, tangent)); // Bitangent from cross product
    }

    // Transform tangent-space normal to object/world space
    mat3 TBN = mat3(tangent, bitangent, norm);
    vec3 perturbedNormal = normalize(TBN * tangentNormal);

    return perturbedNormal;
}

HitInfo RaySphere(Ray ray, vec3 sphereCenter, float sphereRadius) {
    HitInfo hitInfo;
    hitInfo.didHit = false;
    vec3 offsetRayOrigin = ray.origin - sphereCenter;

    float a = dot(ray.dir, ray.dir);
    float b = 2 * dot(offsetRayOrigin, ray.dir);
    float c = dot(offsetRayOrigin, offsetRayOrigin) - sphereRadius * sphereRadius;

    float discriminant = b * b - 4 * a * c;

    if (discriminant >= 0) {
        float dst = (-b - sqrt(discriminant)) / (2 * a);

        if (dst >= 0) {
            hitInfo.didHit = true;
            hitInfo.dst = dst;
            hitInfo.hitPoint = ray.origin + ray.dir * dst;
            hitInfo.normal = normalize(hitInfo.hitPoint - sphereCenter);
            hitInfo.uv = getSphereUV(hitInfo.hitPoint, sphereCenter);
        }
    }
    return hitInfo;
}

HitInfo RayCube(Ray ray, Cube cube) {
    HitInfo info;
    info.didHit = false;

    vec3 invDir = 1.0 / ray.dir; // Inverse of the ray direction
    vec3 t0s = (cube.min.xyz - ray.origin) * invDir;
    vec3 t1s = (cube.max.xyz - ray.origin) * invDir;

    vec3 tSmaller = min(t0s, t1s); // Take the smaller t for each axis
    vec3 tLarger = max(t0s, t1s); // Take the larger t for each axis

    float tMin = max(tSmaller.x, max(tSmaller.y, tSmaller.z)); // Maximum of the minimum t values
    float tMax = min(tLarger.x, min(tLarger.y, tLarger.z)); // Minimum of the maximum t values

    if (tMin <= tMax && tMax > 0.0) {
        info.didHit = true;
        info.dst = tMin;
        info.hitPoint = ray.origin + ray.dir * (tMin - 0.00001);

        // Determine which axis contributed to the closest hit
        vec3 hitNormal = vec3(0.0);
        vec2 uv = vec2(0.0);
        if (tMin == tSmaller.x) {
            hitNormal = vec3(-sign(ray.dir.x), 0.0, 0.0);
            uv = (ray.origin + tMin * ray.dir).yz; // Project onto the YZ plane
            uv = (uv - cube.min.yz) / (cube.max.yz - cube.min.yz);
        } else if (tMin == tSmaller.y) {
            hitNormal = vec3(0.0, -sign(ray.dir.y), 0.0);
            uv = (ray.origin + tMin * ray.dir).xz; // Project onto the XZ plane
            uv = (uv - cube.min.xz) / (cube.max.xz - cube.min.xz); // Normalize to [0, 1]
        } else if (tMin == tSmaller.z) {
            hitNormal = vec3(0.0, 0.0, -sign(ray.dir.z));
            uv = (ray.origin + tMin * ray.dir).xy; // Project onto the XY plane
            uv = (uv - cube.min.xy) / (cube.max.xy - cube.min.xy); // Normalize to [0, 1]
        }

        info.normal = normalize(hitNormal);
        info.uv = uv;
    }

    return info;
}

HitInfo CalculateRayCollision(Ray ray) {
    HitInfo closestHit;
    closestHit.color = vec4(0.0);
    closestHit.dst = 1000.0;

    for (int i = 0; i < objects.length(); i++) {
        // Intersect ray with a sphere
        Sphere sphere = objects[i];
        HitInfo info = RaySphere(ray, sphere.origin.xyz, sphere.info.x);

        // Output the result
        if (info.didHit && info.dst < closestHit.dst) {
            closestHit = info;
            closestHit.color = sphere.color;
            closestHit.info = sphere.info;
        }
    }

    for (int i = 0; i < cubes.length(); i++) {
        // Intersect ray with a cube
        Cube cube = cubes[i];
        HitInfo info = RayCube(ray, cube);

        // Output the result
        if (info.didHit && info.dst < closestHit.dst) {
            closestHit = info;
            closestHit.color = cube.color;
            closestHit.info = cube.info;
        }
    }

    return closestHit;
}

vec3 DisneyDiffuse(vec3 norm, vec3 viewDir, vec3 lightDir, vec3 albedo, float roughness) {
    // Compute angles
    float cosThetaI = max(dot(norm, lightDir), 0.0); // Incident angle
    float cosThetaO = max(dot(norm, viewDir), 0.0); // Outgoing angle

    // Half-vector
    vec3 halfVector = normalize(lightDir + viewDir);

    // Retro-reflection term
    float retroReflection = (1.0 - cosThetaO) * (1.0 - cosThetaI);
    retroReflection *= (1.0 - 0.5 * roughness);

    // Disney Diffuse BRDF
    vec3 diffuse = albedo / 3.141592;
    float retroFactor = 1.0 + retroReflection * pow(1.0 - cosThetaI, 5.0) * roughness;
    return diffuse * retroFactor;
}

vec3 SampleBRDF(vec3 norm, vec3 dir, vec2 uv, inout uint rngState) {
    return normalize(norm + RandomDirection(rngState));
}

float CalculateBRDFPDF(vec3 norm, vec3 dir, vec3 sampleDir, vec2 uv) {
    return max(0.0, dot(norm, sampleDir)) / 3.141592; // Cosine-weighted PDF
}

vec3 EvaluateBRDF(vec3 norm, vec3 viewDir, vec3 lightDir, vec2 uv) {
    vec3 albedo = texture(uDiff, uv).rgb;
    float roughness = texture(uRough, uv).r;
    return DisneyDiffuse(norm, viewDir, lightDir, albedo, roughness);
}

vec3 Trace(Ray ray, inout uint rngState) {
    vec3 incommingLight = vec3(0);
    vec3 rayColor = vec3(1);

    for (int i = 0; i < uMaxBounce; i++) {
        HitInfo info = CalculateRayCollision(ray);
        if (info.didHit) {
            ray.origin = info.hitPoint;
            vec3 norm = applyNormalMap(info.normal, info.uv);
            vec3 emittedLight = info.color.xyz * info.info.y;

            incommingLight += emittedLight * rayColor;

            // Diffuse
            vec3 brdfSampleDir = SampleBRDF(norm, ray.dir, info.uv, rngState);
            float brdfPDF = CalculateBRDFPDF(norm, ray.dir, brdfSampleDir, info.uv);
            vec3 brdfValue = EvaluateBRDF(norm, ray.dir, brdfSampleDir, info.uv);

            ray.dir = brdfSampleDir;
            rayColor *= brdfValue * dot(norm, ray.dir) / brdfPDF;

            // Terminate if ray contribution is negligible
            if (rayColor == vec3(0)) break;
        } else {
            break;
        }
    }

    return incommingLight;
}

vec4 Render(ivec2 texelCoord) {
    vec2 uv = (vec2(texelCoord) + 0.5) / vec2(uRes) * 2.0 - 1.0;

    float aspectRatio = float(uRes.x) / float(uRes.y);
    uv.x *= aspectRatio;

    float scale = tan(radians(uFOV) * 0.5);

    uint pixelIndex = texelCoord.y * uRes.x + texelCoord.x;
    uint rngState = pixelIndex + uint(uTime) * 719393;

    vec3 totalIncomingLight = vec3(0);

    for (int i = 0; i < uRayPerPixel; i++) {
        Ray ray;
        ray.origin = uCamPos;

        vec2 jitter = RandomPointInCircle(rngState) * 2.5;
        vec2 jitteredUV = uv + jitter / vec2(uRes);
        vec3 jitteredDir = normalize(uCamMat * vec3(jitteredUV * scale, -1.0));

        ray.dir = jitteredDir;

        totalIncomingLight += Trace(ray, rngState);
    }

    vec3 pixelCol = totalIncomingLight / uRayPerPixel;

    return vec4(pixelCol, 1.0);
}

void main() {
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);

    vec4 result = Render(texelCoord);
    result = vec4(pow(result.xyz, vec3(0.4545)), 1.0);

    imageStore(imgOutput, texelCoord, result);
}
